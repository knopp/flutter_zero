# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_ios)

import("//build/config/darwin/darwin_sdk.gni")
import("//build/toolchain/clang.gni")
import("//build/toolchain/toolchain.gni")
import("//flutter/common/config.gni")
import("//flutter/shell/config.gni")
import("//flutter/shell/platform/darwin/common/framework_common.gni")
import("//flutter/testing/testing.gni")

_flutter_framework_dir = "$root_out_dir/Flutter.framework"

# The headers that will be copied to the Flutter.framework and be accessed
# from outside the Flutter engine source root.
_flutter_framework_headers = [
  "framework/Headers/Flutter.h",
  "framework/Headers/FlutterAppDelegate.h",
  "framework/Headers/FlutterCallbackCache.h",
  "framework/Headers/FlutterEngine.h",
  "framework/Headers/FlutterEngineGroup.h",
  "framework/Headers/FlutterHeadlessDartRunner.h",
  "framework/Headers/FlutterPlugin.h",
  "framework/Headers/FlutterPluginAppLifeCycleDelegate.h",
  "framework/Headers/FlutterSceneDelegate.h",
  "framework/Headers/FlutterSceneLifeCycle.h",
]

_flutter_framework_headers_copy_dir = "$_flutter_framework_dir/Headers"

source_set("InternalFlutterSwift") {
  # Targets should only ever depend on the framework target.
  # This allows code in this target to use types declared in the bridging
  # header, but defined in the framework, while avoiding linking errors.
  visibility = [ ":flutter_framework_source" ]
  configs += [ "//flutter/shell/platform/darwin/common:config" ]
  include_dirs = [
    "//flutter/shell/platform/darwin/common/framework/Headers",
    "//flutter/shell/platform/darwin/ios/framework/Headers",
    "//flutter/shell/platform/darwin/ios/framework",  # For module.modulemap
  ]
  bridge_header = "InternalFlutterSwift-Bridging-Header.h"
  sources = [ "framework/Source/ConnectionCollection.swift" ]
  public = _flutter_framework_headers + framework_common_headers
}

source_set("flutter_framework_source") {
  visibility = [ ":*" ]
  cflags_objc = flutter_cflags_objc
  cflags_objcc = flutter_cflags_objcc

  defines = [ "FLUTTER_FRAMEWORK=1" ]
  public_configs = [ "//flutter:config" ]
  configs += [
    "//flutter/shell/platform/darwin/common:config",
    "//build/config/ios:ios_application_extension",
  ]

  sources = [
    "framework/Source/FlutterAppDelegate.mm",
    "framework/Source/FlutterAppDelegate_Internal.h",
    "framework/Source/FlutterCallbackCache.mm",
    "framework/Source/FlutterCallbackCache_Internal.h",
    "framework/Source/FlutterDartProject.mm",
    "framework/Source/FlutterDartProject_Internal.h",
    "framework/Source/FlutterDartVMServicePublisher.h",
    "framework/Source/FlutterDartVMServicePublisher.mm",
    "framework/Source/FlutterEngine.mm",
    "framework/Source/FlutterEngineGroup.mm",
    "framework/Source/FlutterEngine_Internal.h",
    "framework/Source/FlutterHeadlessDartRunner.mm",
    "framework/Source/FlutterLaunchEngine.h",
    "framework/Source/FlutterLaunchEngine.m",
    "framework/Source/FlutterPluginAppLifeCycleDelegate.mm",
    "framework/Source/FlutterSceneDelegate.mm",
    "framework/Source/FlutterSceneLifeCycle.mm",
    "framework/Source/FlutterSceneLifeCycle_Internal.h",
    "framework/Source/FlutterSharedApplication.h",
    "framework/Source/FlutterSharedApplication.mm",
    "framework/Source/UIViewController+FlutterScreenAndSceneIfLoaded.h",
    "framework/Source/UIViewController+FlutterScreenAndSceneIfLoaded.mm",
    "framework/Source/platform_message_response_darwin.h",
    "framework/Source/platform_message_response_darwin.mm",
    "framework/Source/profiler_metrics_ios.h",
    "framework/Source/profiler_metrics_ios.mm",
    "platform_message_handler_ios.h",
    "platform_message_handler_ios.mm",
    "platform_view_ios.h",
    "platform_view_ios.mm",
  ]
  sources += _flutter_framework_headers

  frameworks = [
    "AudioToolbox.framework",
    "CoreMedia.framework",
    "CoreVideo.framework",
    "IOSurface.framework",
    "QuartzCore.framework",
    "WebKit.framework",
    "UIKit.framework",
  ]
  if (flutter_runtime_mode == "profile" || flutter_runtime_mode == "debug") {
    # This is required by the profiler_metrics_ios.mm to get GPU statistics.
    # Usage in release builds will cause rejection from the App Store.
    frameworks += [ "IOKit.framework" ]
  }

  deps = [
    "//flutter/common",
    "//flutter/fml",
    "//flutter/lib/ui",
    "//flutter/runtime",
    "//flutter/shell/common",
    "//flutter/shell/platform/darwin/common",
    "//flutter/shell/platform/darwin/common:framework_common",
    "//flutter/shell/platform/embedder:embedder_as_internal_library",
    "//flutter/shell/profiling",
    "//flutter/third_party/icu",
    "//flutter/third_party/spring_animation",
  ]
  public_deps = [ ":InternalFlutterSwift" ]
}

if (enable_ios_unittests) {
  source_set("ios_test_flutter_swift") {
    testonly = true
    visibility = [ ":*" ]
    configs += [ "//flutter/shell/platform/darwin/common:test_config" ]
    sources = [ "framework/Source/ConnectionCollectionTest.swift" ]
    frameworks = [ "XCTest.framework" ]
    deps = [ ":flutter_framework_source" ]
  }

  shared_library("ios_test_flutter") {
    testonly = true
    visibility = [ "*" ]

    cflags_objc = flutter_cflags_objc
    cflags_objcc = flutter_cflags_objcc
    cflags = [
      "-fvisibility=default",
      "-mios-simulator-version-min=$ios_testing_deployment_target",
    ]

    ldflags =
        [ "-Wl,-install_name,@rpath/Frameworks/libios_test_flutter.dylib" ]
    frameworks = [ "XCTest.framework" ]
    include_dirs = [ target_gen_dir ]

    configs += [ "//flutter/shell/platform/darwin/common:test_config" ]
    configs -= [
      "//build/config/gcc:symbol_visibility_hidden",
      "//build/config:symbol_visibility_hidden",
    ]

    sources = [
      "framework/Source/FlutterAppDelegateTest.mm",
      "framework/Source/FlutterDartProjectTest.mm",
      "framework/Source/FlutterEngineGroupTest.mm",
      "framework/Source/FlutterEnginePlatformViewTest.mm",
      "framework/Source/FlutterEngineTest.mm",
      "framework/Source/FlutterLaunchEngineTest.mm",
      "framework/Source/FlutterPluginAppLifeCycleDelegateTest.mm",
      "framework/Source/FlutterSceneDelegateTest.m",
      "framework/Source/FlutterSceneLifeCycleTest.mm",
      "framework/Source/FlutterSharedApplicationTest.mm",
      "framework/Source/UIViewController_FlutterScreenAndSceneIfLoadedTest.mm",
      "framework/Source/availability_version_check_test.mm",
      "platform_message_handler_ios_test.mm",
    ]
    deps = [
      ":flutter_framework",
      ":flutter_framework_source",
      ":ios_test_flutter_swift",
      "//flutter/common",
      "//flutter/fml",
      "//flutter/lib/ui",
      "//flutter/shell/common",
      "//flutter/shell/platform/darwin/common:framework_common",
      "//flutter/shell/platform/embedder:embedder_as_internal_library",
      "//flutter/shell/platform/embedder:embedder_test_utils",
      "//flutter/third_party/ocmock:ocmock_shared",
      "//flutter/third_party/rapidjson",
      "//flutter/third_party/spring_animation",
      "//flutter/third_party/tonic",
    ]
    public_configs = [ "//flutter:config" ]
  }
}  # if (enable_ios_unittests)

shared_library("create_flutter_framework_dylib") {
  visibility = [ ":*" ]

  output_name = "Flutter"

  ldflags = [ "-Wl,-install_name,@rpath/Flutter.framework/Flutter" ]

  public = _flutter_framework_headers

  deps = [ ":flutter_framework_source" ]

  public_configs = [ "//flutter:config" ]
  configs += [ "//build/config/ios:ios_application_extension" ]
}

copy("copy_dylib") {
  visibility = [ ":*" ]

  sources = [ "$root_out_dir/libFlutter.dylib" ]
  outputs = [ "$_flutter_framework_dir/Flutter" ]

  deps = [ ":create_flutter_framework_dylib" ]
}

copy("copy_asan_runtime_dylib") {
  visibility = [ ":*" ]

  _libclang_base_path =
      "$buildtools_path/mac-x64/clang/lib/clang/13.0.0/lib/darwin/"

  if (defined(use_ios_simulator) && use_ios_simulator) {
    _dylib_name = "libclang_rt.asan_iossim_dynamic.dylib"
  } else {
    _dylib_name = "libclang_rt.asan_ios_dynamic.dylib"
  }

  sources = [ "$_libclang_base_path/$_dylib_name" ]
  outputs = [ "$root_out_dir/$_dylib_name" ]

  deps = [ ":create_flutter_framework_dylib" ]
}

action("copy_framework_info_plist") {
  script = "//flutter/build/copy_info_plist.py"
  visibility = [ ":*" ]
  sources = [ "framework/Info.plist" ]
  outputs = [ "$_flutter_framework_dir/Info.plist" ]
  args = [
    "--source",
    rebase_path(sources[0]),
    "--destination",
    rebase_path(outputs[0]),
    "--minversion",
    ios_deployment_target,
    "--buildmode",
    flutter_runtime_mode,
  ]
}

copy("copy_framework_module_map") {
  visibility = [ ":*" ]
  sources = [ "framework/module.modulemap" ]
  outputs = [ "$_flutter_framework_dir/Modules/module.modulemap" ]
}

# Copy privacy manifest. This file is required by Apple for third-party SDKs,
# and documents engine and third_party usage of timestamps and boot time APIs.
# See https://developer.apple.com/documentation/bundleresources/privacy_manifest_files
copy("copy_framework_privacy_manifest") {
  visibility = [ ":*" ]
  sources = [ "framework/PrivacyInfo.xcprivacy" ]
  outputs = [ "$_flutter_framework_dir/PrivacyInfo.xcprivacy" ]
}

action("copy_framework_headers") {
  script = "//flutter/sky/tools/install_framework_headers.py"
  visibility = [ ":*" ]
  sources = get_path_info(_flutter_framework_headers, "abspath") +
            framework_common_headers
  outputs = []
  foreach(header, sources) {
    header_basename = get_path_info(header, "file")
    outputs += [ "$_flutter_framework_headers_copy_dir/$header_basename" ]
  }
  args = [
           "--location",
           rebase_path("$_flutter_framework_headers_copy_dir"),
           "--headers",
         ] + rebase_path(sources, "", "//")
}

copy("copy_framework_icu") {
  visibility = [ ":*" ]
  sources = [ "//flutter/third_party/icu/flutter/icudtl.dat" ]
  outputs = [ "$_flutter_framework_dir/{{source_file_part}}" ]
}

copy("copy_license") {
  visibility = [ ":*" ]
  sources = [ "//LICENSE" ]
  outputs = [ "$root_out_dir/LICENSE" ]
}

shared_library("copy_and_verify_framework_module") {
  framework_search_path = rebase_path("$root_out_dir")
  visibility = [ ":*" ]
  cflags_objc = [ "-F$framework_search_path" ]
  ldflags = [
    "-F$framework_search_path",
    "-Xlinker",
    "-fatal_warnings",
  ]

  sources = [ "framework/Source/FlutterUmbrellaImport.m" ]
  deps = [
    ":copy_framework_headers",
    ":copy_framework_info_plist",
    ":copy_framework_module_map",
    ":copy_framework_privacy_manifest",
  ]

  configs += [ "//build/config/ios:ios_application_extension" ]
}

group("universal_flutter_framework") {
  visibility = [ ":*" ]
  deps = [
    ":copy_and_verify_framework_module",
    ":copy_dylib",
    ":copy_framework_icu",
    ":copy_framework_info_plist",
    ":copy_framework_module_map",
    ":copy_framework_privacy_manifest",
    ":copy_license",
  ]

  if (is_asan) {
    deps += [ ":copy_asan_runtime_dylib" ]
  }
}

action("flutter_framework") {
  script = "//flutter/sky/tools/create_xcframework.py"
  outputs = [ "$root_out_dir/Flutter.xcframework" ]
  args = [
    "--frameworks",
    rebase_path("$_flutter_framework_dir"),
    "--name",
    "Flutter",
    "--location",
    rebase_path("$root_out_dir"),
  ]

  deps = [ ":universal_flutter_framework" ]
}
